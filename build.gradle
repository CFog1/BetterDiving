buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = "$mcVersion-$modVersion"
group = 'meldexun.better_diving'
archivesBaseName = "$modName"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: '1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runClient BD 1.16'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                better_diving {
                    source sourceSets.main
                }
            }
        }

        server {
            taskName 'runServer BD 1.16'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                better_diving {
                    source sourceSets.main
                }
            }
        }

        data {
            taskName 'runData BD 1.16'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            args '--mod', 'better_diving', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                better_diving {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    includedDependencies

    implementation.extendsFrom(includedDependencies)
}

repositories {
    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() } 
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.31'
    includedDependencies 'meldexun:ReflectionUtil:1.0.0@jar'
}

jar {
    from {
        configurations.includedDependencies.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jar.finalizedBy('reobfJar') 

task apiJar(type: Jar) {
    archiveClassifier.set('api')
    include 'meldexun/better_diving/api/**/*'
    from sourceSets.main.allSource
    from sourceSets.main.output
}

tasks.build.dependsOn apiJar
